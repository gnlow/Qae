@top Program { Statement* }

@precedence {
  call,

  times @left,
  plus @left,

  rel @left
}

kw<term> { @specialize[@name={term}]<Identifier, term> }

Statement {
  FunctionDef |
  IfStatement |
  Assignment |
  ExpressionStatement
}

FunctionDef {
  kw<"fun"> Identifier "(" (Identifier ("," Identifier)*)? ")" Block
}

IfStatement {
  kw<"if"> "(" Expression ")" Block
  (kw<"else"> Block)?
}

Assignment {
  Identifier (At { "[" Expression "]" })?
  "=" Expression
}

ExpressionStatement {
  Expression
}

Block {
  "{" Statement* "}"
}

Expression {
  FunctionCall |
  BinaryExpression |
  String |
  IdentifierExpression |
  Number |
  "(" Expression ")"
}

IdentifierExpression {
  Identifier (At { "[" Expression "]" })?
}

CompareOp { "<" | "<=" | ">" | ">=" | "==" | "!=" }

BinaryExpression {
  Expression !times Op { "/" | "%" | "*" } Expression |
  Expression !plus Op { "+" | "-" } Expression |
  Expression !rel Op { CompareOp } Expression
}

FunctionCall {
  Identifier !call"(" (Expression ("," Expression)*)? ")"
}

@skip {} {
  String {
    "\"" stringContent* stringEnd
  }
}

@local tokens {
  stringEnd { "\"" }
  @else stringContent
}

@tokens {
  Identifier { $[a-zA-Z_] $[a-zA-Z0-9_]* }
  Number { $[0-9]+ }
  space { $[ \t\n]+ }

  assign { "=" }
  "=="
  comma { "," }
  lparen { "(" }
  rparen { ")" }
  lbrace { "{" }
  rbrace { "}" }
  lsq { "[" }
  rsq { "]" }
}

@skip { space }
